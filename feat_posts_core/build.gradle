plugins {
    id "com.android.library"
    id "kotlin-android"
    id "kotlin-android-extensions"
    id "io.gitlab.arturbosch.detekt" version "1.3.0"
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.5'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

android {
    buildToolsVersion "29.0.2"
    compileSdkVersion 29


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    flavorDimensions "data"
    productFlavors {
        live {
            dimension "data"
        }
        mock {
            dimension "data"
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main { java.srcDirs = ['src/main/kotlin'] }
        live { java.srcDirs = ['src/live/kotlin'] }
        mock { java.srcDirs = ['src/mock/kotlin'] }
        test { java.srcDirs = ['src/test/kotlin'] }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        animationsDisabled true
        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
        unitTests {
            includeAndroidResources = true
        }
    }

}

detekt {
    toolVersion = "1.3.0"
    input = files(
            "src/main/kotlin",
            "src/live/kotlin",
            "src/mock/kotlin"
    )
    parallel = true
    reports {
        xml {
            enabled = true
            destination = file("build/reports/detekt.xml")
        }
        html {
            enabled = true
            destination = file("build/reports/detekt.html")
        }
    }
}

dependencies {
    implementation project(':shared_database')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "androidx.core:core-ktx:1.1.0"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-rc03'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-rc03'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-rc03'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation "org.koin:koin-android:2.0.1"
    implementation "org.koin:koin-androidx-scope:2.0.1"
    implementation "org.koin:koin-androidx-viewmodel:2.0.1"

    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:1.0'

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:2.0.9"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:2.0.9"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"


    androidTestImplementation 'androidx.test:runner:1.2.0'
}
repositories {
    mavenCentral()
}
